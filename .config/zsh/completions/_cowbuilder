#compdef cowbuilder

_cowbuilder_environments() {
    local -a results
    local distrib
    for cow in /var/cache/pbuilder/base-*.cow(N); do
        distrib=${${${cow:t}%.cow}#base-}
        arch=${(M)${(f)"$(dpkg-architecture -L)"}:#${distrib##*-}}
        [[ -z $arch ]] || distrib=${distrib%-*}/$arch
        results=($results $distrib)
    done
    _wanted base expl 'base path' compadd -a \
	results
}

local context state state_descr line expl
typeset -A opt_args

_arguments \
    '1:::_cowbuilder_environments' \
    '2:action:((--create\:"create new environment"
                --build\:"build a package"
                --update\:"update environment"
                --login\:"login into environment"
                --execute\:"execute a command"))' \
    '*:: :->subcmds' \
    && return 0

case "$words[2]" in
    --build)
	if (( CURRENT == 3 )); then
	    _wanted dsc expl "dsc file" _files -g "*.dsc"
	fi
	;;
    --execute)
	precommands+=($words[2])
	shift 2 words
	(( CURRENT -= 2 ))
	_normal
	;;
esac

return 0

# Local Variables:
# mode: Shell-Script
# sh-indentation: 4
# indent-tabs-mode: nil
# sh-basic-offset: 4
# End:
