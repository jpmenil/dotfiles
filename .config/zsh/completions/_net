#compdef net
#
# zsh completion for Cumulus "net" tool.
#
# Completion can be built from:
#  "net show int --completions 2" -> "interface"
#  "net show i --completions 2" -> "igmp    :  Internet Group Management Protocol"
#

__net_command () {
    emulate -L zsh
    setopt extendedglob
    local -a completions
    completions=(${(f)"$(_call_program commands net ${words} --completions $CURRENT 2>&1)"})
    if (( $#completions > 1 )); then
        # We have descriptions too
        completions=(${(M)completions:#*  :  *})
        completions=(${completions## #})
        completions=(${completions/   #:  /:})
        completions=(${completions:#<*})
    fi
    _describe -t net-command "net completion" completions "$@"
}

_net () {
    local curcontext="$curcontext" state line

    _arguments -C \
        '(-)*::net command:__net_command'
}


_net "$@"

# Local Variables:
# mode: Shell-Script
# sh-indentation: 4
# indent-tabs-mode: nil
# sh-basic-offset: 4
# End:
